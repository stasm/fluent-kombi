line_end                   ::= "\u000D\u000A"
                             | "\u000A"
                             | "\u000D"
                             | EOF
inline_space               ::= ("\u0020" | "\u0009")+
blank_line                 ::= inline_space? line_end
break_indent               ::= line_end blank_line* inline_space
digit                      ::= [0-9]
number                     ::= "-"? digit+ ("." digit+)?
other_char                 ::= [!-\uD7FF\uE000-\uFFFD]
backslash                  ::= "\\"
quote                      ::= "\""
text_char                  ::= inline_space
                             | /\\u[0-9a-fA-F]{4}/
                             | (backslash backslash)
                             | (backslash "{")
                             | (other_char - "{" - backslash)
text_cont                  ::= break_indent (- "}" - "[" - "*" - ".")
TextElement                ::= (text_char | text_cont)+
quoted_text_char           ::= (text_char - quote)
                             | (backslash quote)
quoted_text                ::= quote quoted_text_char* quote
identifier                 ::= [a-zA-Z] [a-zA-Z0-9_-]*
Identifier                 ::= identifier
TermIdentifier             ::= "-" identifier
ExternalIdentifier         ::= "$" identifier
Function                   ::= [A-Z] [A-Z_?-]*
StringExpression           ::= quoted_text
NumberExpression           ::= number
NamedArgument              ::= Identifier (inline_space? ":" inline_space?) (StringExpression | NumberExpression)
Argument                   ::= inline_space? (NamedArgument | InlineExpression) inline_space?
argument_list              ::= Argument ("," Argument)* ","?
CallExpression             ::= Function "(" (inline_space? argument_list? inline_space?) ")"
MessageAttributeExpression ::= Identifier "." Identifier
TermAttributeExpression    ::= TermIdentifier "." Identifier
TermVariantExpression      ::= TermIdentifier "[" (inline_space? VariantKey inline_space?) "]"
InlineExpression           ::= StringExpression
                             | NumberExpression
                             | CallExpression
                             | MessageAttributeExpression
                             | TermVariantExpression
                             | Identifier
                             | TermIdentifier
                             | ExternalIdentifier
                             | Placeable
SelectorExpression         ::= StringExpression
                             | NumberExpression
                             | CallExpression
                             | TermAttributeExpression
                             | ExternalIdentifier
word                       ::= (other_char - backslash - "}" - "{" - "]" - "[")+
VariantName                ::= word (inline_space word)*
VariantKey                 ::= NumberExpression
                             | VariantName
Variant                    ::= break_indent "[" (inline_space? VariantKey inline_space?) "]" inline_space? Pattern
DefaultVariant             ::= break_indent "*" "[" (inline_space? VariantKey inline_space?) "]" inline_space? Pattern
variant_list               ::= Variant* DefaultVariant Variant*
SelectExpression           ::= SelectorExpression (inline_space? "->" inline_space?) variant_list break_indent
VariantList                ::= variant_list break_indent
BlockExpression            ::= SelectExpression
                             | VariantList
Placeable                  ::= "{" (inline_space? (BlockExpression | InlineExpression) inline_space?) "}"
Pattern                    ::= inline_space? text_cont? (TextElement | Placeable)+
Attribute                  ::= break_indent "." Identifier (inline_space? "=" inline_space?) Pattern
Message                    ::= Comment? Identifier (inline_space? "=" inline_space?) ((Pattern Attribute*) | (Attribute+)) line_end
Term                       ::= Comment? TermIdentifier (inline_space? "=" inline_space?) Pattern Attribute* line_end
comment_line               ::= (line_end)
                             | ("\u0020" /.*/ line_end)
Comment                    ::= ("#" comment_line)+
GroupComment               ::= ("##" comment_line)+
ResourceComment            ::= ("###" comment_line)+
Junk                       ::= ((/.*/ line_end) - Comment - GroupComment - ResourceComment - Term - Message)+
Entry                      ::= Message
                             | Term
                             | (ResourceComment | GroupComment | Comment)
                             | Junk
Resource                   ::= (blank_line | Entry)*
