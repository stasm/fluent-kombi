Resource                   ::= (blank_line | Entry)*
Entry                      ::= Message
                             | Term
                             | (ResourceComment | GroupComment | Comment)
                             | Junk
Junk                       ::= ((/.*/ line_end) - Comment - GroupComment - ResourceComment - Term - Message)+
ResourceComment            ::= ("###" comment_line)+
GroupComment               ::= ("##" comment_line)+
Comment                    ::= ("#" comment_line)+
comment_line               ::= (line_end)
                             | ("\u0020" /.*/ line_end)
Term                       ::= Comment? TermIdentifier (inline_space? "=" inline_space?) Pattern Attribute* line_end
Message                    ::= Comment? Identifier (inline_space? "=" inline_space?) ((Pattern Attribute*) | (Attribute+)) line_end
Attribute                  ::= break_indent "." Identifier (inline_space? "=" inline_space?) Pattern
Pattern                    ::= inline_space? text_cont? (TextElement | Placeable)+
TextElement                ::= (text_char | text_cont)+
Placeable                  ::= "{" (inline_space? (BlockExpression | InlineExpression) inline_space?) "}"
BlockExpression            ::= SelectExpression
                             | VariantList
VariantList                ::= variant_list break_indent
SelectExpression           ::= SelectorExpression (inline_space? "->" inline_space?) variant_list break_indent
variant_list               ::= Variant* DefaultVariant Variant*
DefaultVariant             ::= break_indent "*" "[" (inline_space? VariantKey inline_space?) "]" inline_space? Pattern
Variant                    ::= break_indent "[" (inline_space? VariantKey inline_space?) "]" inline_space? Pattern
VariantKey                 ::= NumberExpression
                             | VariantName
VariantName                ::= word (inline_space word)*
word                       ::= (other_char - backslash - "}" - "{" - "]" - "[")+
SelectorExpression         ::= StringExpression
                             | NumberExpression
                             | CallExpression
                             | TermAttributeExpression
                             | ExternalIdentifier
InlineExpression           ::= StringExpression
                             | NumberExpression
                             | CallExpression
                             | MessageAttributeExpression
                             | TermVariantExpression
                             | Identifier
                             | TermIdentifier
                             | ExternalIdentifier
                             | Placeable
TermVariantExpression      ::= TermIdentifier "[" (inline_space? VariantKey inline_space?) "]"
TermAttributeExpression    ::= TermIdentifier "." Identifier
MessageAttributeExpression ::= Identifier "." Identifier
CallExpression             ::= Function "(" (inline_space? argument_list? inline_space?) ")"
argument_list              ::= Argument ("," Argument)* ","?
Argument                   ::= inline_space? (NamedArgument | InlineExpression) inline_space?
NamedArgument              ::= Identifier (inline_space? ":" inline_space?) (StringExpression | NumberExpression)
NumberExpression           ::= number
StringExpression           ::= quoted_text
Function                   ::= [A-Z] [A-Z_?-]*
ExternalIdentifier         ::= "$" identifier
TermIdentifier             ::= "-" identifier
Identifier                 ::= identifier
identifier                 ::= [a-zA-Z] [a-zA-Z0-9_-]*
quoted_text                ::= quote quoted_text_char* quote
quoted_text_char           ::= (text_char - quote)
                             | (backslash quote)
text_cont                  ::= break_indent (- "}" - "[" - "*" - ".")
text_char                  ::= inline_space
                             | /\\u[0-9a-fA-F]{4}/
                             | (backslash backslash)
                             | (backslash "{")
                             | (other_char - "{" - backslash)
quote                      ::= "\""
backslash                  ::= "\\"
// Any Unicode character from BMP excluding C0 control characters, space,
// surrogate blocks and non-characters (U+FFFE, U+FFFF).
// Cf. https://www.w3.org/TR/REC-xml/#NT-Char
// TODO Add characters from other planes: U+10000 to U+10FFFF.
other_char                 ::= [!-\uD7FF\uE000-\uFFFD]
number                     ::= "-"? digit+ ("." digit+)?
digit                      ::= [0-9]
break_indent               ::= line_end blank_line* inline_space
blank_line                 ::= inline_space? line_end
inline_space               ::= ("\u0020" | "\u0009")+
line_end                   ::= "\u000D\u000A"
                             | "\u000A"
                             | "\u000D"
                             | EOF
